%(G) A son-in-law, daughter-in-law, father-in-law, mother-in-law, brother-in-law, or sister-in-law.
s152_d_2_G(Dependent,Taxp,Start_day,End_day) :-
    is_child_in_law_of(Dependent,Taxp,Start_day,End_day).
s152_d_2_G(Dependent,Taxp,Start_day,End_day) :-
    is_parent_in_law_of(Dependent,Taxp,Start_day,End_day).
s152_d_2_G(Dependent,Taxp,Start_day,End_day) :-
    is_sibling_in_law_of(Dependent,Taxp,Start_day,End_day).

is_child_in_law_of(X,Y,Day_start,Day_end) :- % X is child in law of Y if
	is_child_of(ChildY,Y,Day_start_y,Day_end_y), % one of the children of Y
	marriage_(Marriage), % got married with X
	agent_(Marriage,ChildY),
	agent_(Marriage,X),
    not_same_person(X,Y),
    wedding_start(Marriage,Start_time),
    later(Day_start,Start_time,Day_start_y),
	wedding_end(Marriage,End_time),
    earlier(Day_end,End_time,Day_end_y).

is_parent_in_law_of(X,Y,Day_start,Day_end) :- % X is parent in law of Y if
	is_child_of(Y,ParentY,Day_start_y,Day_end_y), % one of the parents of Y
	marriage_(Marriage), % got married with X
	agent_(Marriage,ParentY),
	agent_(Marriage,X),
    not_same_person(X,Y),
    wedding_start(Marriage,Start_time),
    later(Day_start,Start_time,Day_start_y),
	wedding_end(Marriage,End_time),
    earlier(Day_end,End_time,Day_end_y).

is_sibling_in_law_of(X,Y,Day_start,Day_end) :- % symmetry of relationship
    is_sibling_in_law_of_aux(X,Y,Day_start,Day_end).
is_sibling_in_law_of(X,Y,Day_start,Day_end) :- % symmetry of relationship
    is_sibling_in_law_of_aux(Y,X,Day_start,Day_end).

is_sibling_in_law_of_aux(X,Y,Day_start,Day_end) :- % X is sibling in law of Y if
	is_sibling_of(Y,SiblingY,Day_start_y,Day_end_y), % one of Y's siblings
	marriage_(Marriage), % and X got married
	agent_(Marriage,SiblingY),
	agent_(Marriage,X),
    not_same_person(X,Y),
    wedding_start(Marriage,Start_time),
    later(Day_start,Start_time,Day_start_y),
	wedding_end(Marriage,End_time),
    earlier(Day_end,End_time,Day_end_y).

relationship(Relationship):-
    agent_(Relationship,_).
relationship(Relationship):-
    patient_(Relationship,_).


relationship_start(Relationship,Day_start):-
    relationship(Relationship),date(Day_start),not_start_(Relationship,_).
relationship_start(Relationship,Day_start):-   
    start_(Relationship,Day_start).

relationship_end(Relationship,Day_end):-
    relationship(Relationship),date(Day_end),not_end_(Relationship,_).
relationship_end(Relationship,Day_end):-
    end_(Relationship,Day_end). 

is_descendent_of(X,Y,Day_start,Day_end) :- % X is a descendent of Y if
	% as a base case, X is a descendent of Y if X is a child of Y
    is_child_of(X,Y,Day_start,Day_end).
is_descendent_of(X,Y,Day_start,Day_end) :- 
	% else
    is_child_of(Z,Y,_,_), % Z is a child of Y and
    is_descendent_of(X,Z,Day_start,Day_end). % X is a descendent of Z

is_child_of(X,Y,Day_start,Day_end) :- % X is the child of Y from Day_start to Day_end if...
	% what kind of relationship exists between X and Y
	child_or_parent(X,Y,Relationship),
    not_same_person(X,Y),
	relationship_start(Relationship,Day_start),
	% if no end to Relationship then it's still ongoing
    relationship_end(Relationship,Day_end).

child_or_parent(X,Y,Relationship):-
    % X is child of Y, or
    son_or_daughter(Relationship),
    agent_(Relationship,X),
    patient_(Relationship,Y).
child_or_parent(X,Y,Relationship):-
    % Y is parent of X
    father_or_mother(Relationship),
    agent_(Relationship,Y),
    patient_(Relationship,X).

son_or_daughter(Relationship):-
    son_(Relationship).
son_or_daughter(Relationship):-
    daughter_(Relationship).

father_or_mother(Relationship):-
    father_(Relationship).
father_or_mother(Relationship):-
    mother_(Relationship).

is_sibling_of(X,Y,Day_start,Day_end) :- % X is the sibling of Y on from Day_start to Day_end if...
	% there is a brother or sister relationship
	brother_and_sister(Relationship),
	% X is somehow involved in it
	agent_or_patient(Relationship,X),
	% and so is Y
    agent_or_patient(Relationship,Y),
    not_same_person(X,Y),
	relationship_start(Relationship,Day_start),
	% if no end to Relationship then it's still ongoing
	relationship_end(Relationship,Day_end).

brother_and_sister(Relationship):-
    brother_(Relationship).
brother_and_sister(Relationship):-
    sister_(Relationship).

agent_or_patient(Relationship,X):-
    agent_(Relationship,X).
agent_or_patient(Relationship,X):-
    patient_(Relationship,X).

is_stepsibling_of(X,Y,Day_start,Day_end) :- % X is stepsibling of Y if
	is_child_of(Y,ParentY,Day_start_x,Day_end_x), % one of the parents of Y
	is_child_of(X,ParentX,Day_start_y,Day_end_y), % and one of the parents of X
	marriage_(Marriage), % got married
	agent_(Marriage,ParentY),
	agent_(Marriage,ParentX),
    not_same_person(X,Y),
    wedding_start(Marriage,Start_time),
    later(Day_start_2,Day_start_x,Day_start_y),
    later(Day_start,Day_start_2,Start_time),
    wedding_end(Marriage,End_time),
    earlier(Day_end_2,Day_end_x,Day_end_y),
    earlier(Day_end,Day_end_2,End_time).

wedding_start(Marriage,Day_start):-
    marriage_(Marriage),date(Day_start),not_start_(Marriage,_).
wedding_start(Marriage,Day_start):-   
    start_(Marriage,Day_start).

wedding_end(Marriage,Day_end):-
    marriage_(Marriage),date(Day_end),not_end_(Marriage,_).
wedding_end(Marriage,Day_end):-
    end_(Marriage,Day_end). 

same_person(P1,P2):-
    person(P1),person(P2),P1=P2.

earlier(Result, Date1, Date2) :-
    date(Date1), date(Date2), is_before(Date2,Date1), Result = Date2.
earlier(Result, Date1, Date2) :-
    date(Date1), date(Date2), is_before(Date1,Date2), Result = Date1.
earlier(Result, Date1, Date2) :-
	date(Date1), date(Date2), Date1 = Date2, Result = Date1.

later(Result, Date1, Date2) :-
	date(Date1), date(Date2), is_before(Date2,Date1), Result = Date1.
later(Result, Date1, Date2) :-
	date(Date1), date(Date2), is_before(Date1,Date2), Result = Date2.
later(Result, Date1, Date2) :-
	date(Date1), date(Date2), Date1 = Date2, Result = Date1.

is_before(Day1,Day2):-
    date_split(Day1,Y1,M1,D1),
    date_split(Day2,Y2,M2,D2),
    Y1 < Y2.
is_before(Day1,Day2):-
    date_split(Day1,Y1,M1,D1),
    date_split(Day2,Y2,M2,D2),
    Y1 = Y2,M1 < M2.
is_before(Day1,Day2):-
    date_split(Day1,Y1,M1,D1),
    date_split(Day2,Y2,M2,D2),
    Y1 = Y2,M1 = M2,D1 < D2.

assumption(not_same_person(P1,P2)).
assumption(not_start_(Event,Time)).
assumption(not_end_(Event,Time)).

contrary(not_same_person(P1,P2),same_person(P1,P2)):-
    assumption(not_same_person(P1,P2)).
contrary(not_start_(Event,Time),start_(Event,Time)) :- 
    assumption(not_start_(Event,Time)).
contrary(not_end_(Event,Time),end_(Event,Time)) :- 
    assumption(not_end_(Event,Time)).

% Text
% Alice and Bob started living together on April 15th, 2014. Alice and Bob are not related, nor do they have relatives married to one another.

% Question
% Alice bears a relationship to Bob under section 152(d)(2)(G) for the year 2018. Contradiction

% Facts
person(alice_s152_d_2_G_neg).
person(bob_s152_d_2_G_neg).

year(2014).
date(d2014_04_15).
date_split(d2014_04_15, 2014, 4, 15).
date(d2014_01_01).
date_split(d2014_01_01, 2014, 1, 1).
date(d2014_12_31).
date_split(d2014_12_31, 2014, 12, 31).

year(2100).
date(d2100_01_01).
date_split(d2100_01_01, 2100, 1, 1).

residence_(alice_and_bob_s152_d_2_G_neg).
agent_(alice_and_bob_s152_d_2_G_neg,alice_s152_d_2_G_neg).
agent_(alice_and_bob_s152_d_2_G_neg,bob_s152_d_2_G_neg).
start_(alice_and_bob_s152_d_2_G_neg,d2014_04_15).
patient_(alice_and_bob_s152_d_2_G_neg,home_s152_d_2_G_neg).

% Text
% Charlie is Bob's father since April 15th, 2014. Alice married Bob on October 12th, 1992.

% Question
% Alice bears a relationship to Charlie under section 152(d)(2)(G) for the year 2018. Entailment

% Facts
person(alice_s152_d_2_G_pos).
person(bob_s152_d_2_G_pos).
person(charlie_s152_d_2_G_pos).

year(2014).
date(d2014_04_15).
date_split(d2014_04_15, 2014, 4, 15).
date(d2014_01_01).
date_split(d2014_01_01, 2014, 1, 1).
date(d2014_12_31).
date_split(d2014_12_31, 2014, 12, 31).

year(1992).
date(d1992_10_12).
date_split(d1992_10_12, 1992, 10, 12).
date(d1992_01_01).
date_split(d1992_01_01, 1992, 1, 1).
date(d1992_12_31).
date_split(d1992_12_31, 1992, 12, 31).

year(2018).
date(d2018_01_01).
date_split(d2018_01_01, 2018, 1, 1).
date(d2018_12_31).
date_split(d2018_12_31, 2018, 12, 31).

year(2100).
date(d2100_01_01).
date_split(d2100_01_01, 2100, 1, 1).

father_(charlie_and_bob_s152_d_2_G_pos).
agent_(charlie_and_bob_s152_d_2_G_pos,charlie_s152_d_2_G_pos).
patient_(charlie_and_bob_s152_d_2_G_pos,bob_s152_d_2_G_pos).
start_(charlie_and_bob_s152_d_2_G_pos,d2014_04_15).
marriage_(alice_and_bob_s152_d_2_G_pos).
agent_(alice_and_bob_s152_d_2_G_pos,alice_s152_d_2_G_pos).
agent_(alice_and_bob_s152_d_2_G_pos,bob_s152_d_2_G_pos).
start_(alice_and_bob_s152_d_2_G_pos,d1992_10_12).

%pos:s152_d_2_G(alice_s152_d_2_G_pos,charlie_s152_d_2_G_pos,d2014_04_15,d2100_01_01).
%neg:s152_d_2_G(alice_s152_d_2_G_neg,bob_s152_d_2_G_neg,d2014_04_15,d2100_01_01).

% aba_asp('./experiments/s152_d_2_G_learning.bk',[s152_d_2_G(alice_s152_d_2_G_pos,charlie_s152_d_2_G_pos,d2014_04_15,d2100_01_01)],[s152_d_2_G(alice_s152_d_2_G_neg,bob_s152_d_2_G_neg,d2014_04_15,d2100_01_01)]).

